buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

group = 'com.github.khangnt'
version = '0.1.2'

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "khang-nt"
            name "Khang-NT"
            email "vn.khangnt@gmail.com"
        }
    }

    scm {
        url "https://github.com/Khang-NT/File-Downloader"
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javaDocJar
            groupId group
            artifactId 'file-downloader'
            version '0.1.2'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A File-Downloader library for Android and Java applications.')
                root.appendNode('name', 'File Downloader')
                root.appendNode('url', 'https://github.com/Khang-NT/File-Downloader')
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
    Properties properties = new Properties()
    File localPropsFile = project.rootProject.file('local.properties')
    def bintrayUser = null
    def bintrayApiKey = null
    if (localPropsFile.exists()) {
        properties.load(localPropsFile.newDataInputStream())
        bintrayUser = properties.getProperty('bintrayUser')
        bintrayApiKey = properties.getProperty('bintrayApiKey')
    }
    user = bintrayUser != null ? bintrayUser : System.getenv('BINTRAY_USER')
    key = bintrayApiKey != null ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
    publish = true
    publications = ['Publication']
    pkg {
        repo = 'maven'
        name = 'Java-File-Downloader'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Khang-NT/File-Downloader.git'
        version {
            name = "v0.1.2"
            vcsTag = "release-v0.1.2"
            released  = new Date()
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavaDoc(type: Javadoc) {
    source = sourceSets.main.java.srcDirs
    destinationDir = file("../javadoc/")
    failOnError false
}

task javaDocJar(type: Jar) {
    from javadoc.destinationDir
    classifier 'javadoc'
    dependsOn generateJavaDoc
}

artifacts {
    archives sourceJar
    archives javaDocJar
}